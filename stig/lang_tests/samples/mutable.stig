/* <stig/lang_tests/samples/mutable.stig>

   Some sample mutations in Stig script.

   Copyright 2010-2014 Stig LLC

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

     http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License. */

package #1;

/* type alias */
addr is <[int]>;

/* Create a new empty list of integers at the given address.
   Return true. */
create_list = ((true) effecting {
  new loc <- empty [int];
}) where {
  loc = given::(addr);
};

/* Delete the list of of integers at the given address.
   Return the contents of the list as it was before being destroyed. */
delete_list = ((my_list) effecting {
  delete ([int]) @ &my_list;
}) where {
  loc = given::(addr);
  my_list = *loc::([int]);
};

/* Append a new integer to the list of of integers at the given address.
   Return the contents of the list as it was before being appended to. */
append_to_list = ((my_list) effecting {
  my_list += [val];
}) where {
  loc = given::(addr);
  my_list = *loc::([int]);
  val = given::(int);
};

/* Replace one integer with another in the list of of integers at the given address.
   Return the old value of the index before it was modified. */
assign_to_list = ((my_list[idx]) effecting {
  my_list[idx] <- val;
}) where {
  loc = given::(addr);
  my_list = *loc::([int]);
  val = given::(int);
  idx = given::(int);
};
