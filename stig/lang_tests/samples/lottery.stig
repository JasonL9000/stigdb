/* <stig/lang_tests/samples/lottery.stig>

   Copyright 2010-2014 Stig LLC

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

     http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License. */

package #1;

init_round = (true) effecting {
  new <['num_round']> <- 0;
};

num_round = *<['num_round']>::(int);

increment_round = (true) effecting {
  num_round += 1;
};

test {
  t1: init_round {
    t2: num_round == 0;
  };
};

with {
  <['num_round']> <- 5;
} test {
  t3: num_round == 5 {
    t4: increment_round {
      t5: num_round == 6;
    };
  };
};

init_ticket = (true) effecting {
  new <['num_ticket']> <- 0;
};

reset_ticket = (true) effecting {
  *<['num_ticket']>::(int) <- 0;
};

num_tickets = *<['num_ticket']>::(int);

increment_ticket = (true) effecting {
  num_tickets += 1;
};

test {
  t6: init_ticket {
    t7: num_tickets == 0 {
      t8: increment_ticket {
        t9: num_tickets == 1 {
          t10: reset_ticket {
            t11: num_tickets == 0;
          };
        };
      };
    };
  };
};

with {
  <['num_ticket']> <- 5;
} test {
  t12: num_tickets == 5 {
    t13: increment_ticket {
      t14: num_tickets == 6;
    };
  };
};

new_ticket = ((num_tickets) effecting {
  new <[num_round, num_tickets]> <- user;
}) where {
  user = given::(id);
};

with {
  <['num_round']> <- 3;
  <['num_ticket']> <- 100;
} test {
  t15: new_ticket(.user:{12345678-1234-1234-1234-123456789012}) == 100 {
    t16: *<[num_round, num_tickets]>::(id) == {12345678-1234-1234-1234-123456789012} {
      t17: increment_ticket {
        t18: num_tickets == 101;
      };
    };
  };
};

winner = (*<[num_round, winning_ticket]>::(id)) where {
  winning_ticket = given::(int);
};

with {
  <['num_round']> <- 1;
  <[1, 0]> <- {12345678-1234-1234-1234-123456789012};
  <[1, 1]> <- {22345678-1234-1234-1234-123456789012};
  <[1, 2]> <- {32345678-1234-1234-1234-123456789012};
  <[1, 3]> <- {42345678-1234-1234-1234-123456789012};
} test {
  t19: winner(.winning_ticket: 0) == {12345678-1234-1234-1234-123456789012};
  t20: winner(.winning_ticket: 1) == {22345678-1234-1234-1234-123456789012};
  t21: winner(.winning_ticket: 2) == {32345678-1234-1234-1234-123456789012};
  t22: winner(.winning_ticket: 3) == {42345678-1234-1234-1234-123456789012};
};