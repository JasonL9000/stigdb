/* <stig/lang_tests/samples/degrees.stig>

   Copyright 2010-2014 Stig LLC

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

     http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License. */
package #1;

/*
<[int, str, int int ]> <- bool;
<[from, 'kind', to, time] <- existence
*/

get_degree_of_user = (edges_from_user reduce (start 0) + 1) where {
  edges_from_user = keys (bool) @ <[userid, kind, free::(int), free::(int)]>;
  userid = given::(int);
  kind = given::(str);
};

degree_histogram = (result - {0}) where {
  kind = given::(str);
  all_edges = keys(bool) @ <[free::(int), free::(str), free::(int), free::(int)]>;
  result = agg_hist(.sv:all_edges reduce add_hist(.start_val:start <[empty {int:int}, -1, 0]>, .edge:that));
  add_hist = ((((<[start_val.0, start_val.1, start_val.2 + 1]> /*add count */)
               if (edge.0 == start_val.1) else
               (<[agg_hist(.sv:start_val), edge.0, 1]>/*new_count*/)) if (edge.1 == kind) else (start_val))) where {
    /* <[map, last_id, cur_count]> */
    start_val = given::(<[{int:int}, int, int]>);
    edge = given::(<[int, str, int, int]>);
  };
  agg_hist = (add_to_hist(.hist:sv.0, .degr:sv.2)) where {
    sv = given::(<[{int:int}, int, int]>);
  };
  add_to_hist = ((((hist - {degr} ) + {degr: (hist[degr] + 1)}/* increment entry */) if (degr in hist) else (hist + {degr:1} /* new entry */))) where {
    hist = given::({int:int});
    degr = given::(int);
  };
};

with {
  <[1, "is_friend_of", 2, 3]> <- true;
  <[1, "is_friend_of", 3, 3]> <- true;
  <[2, "is_friend_of", 1, 3]> <- true;
  <[3, "is_friend_of", 1, 3]> <- true;
  <[1, "not connections", 2, 3]> <- true;
}
test {
  t0: get_degree_of_user(.userid:1, .kind:"is_friend_of") == 2;
  t1: get_degree_of_user(.userid:2, .kind:"is_friend_of") == 1;
  t2: get_degree_of_user(.userid:3, .kind:"is_friend_of") == 1;
  t3: degree_histogram(.kind:"is_friend_of") == {1:2, 2:1};
};