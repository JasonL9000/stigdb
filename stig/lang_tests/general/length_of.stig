/* <stig/lang_tests/general/length_of.stig>

   This Stig script tests the length_of operation.

   Copyright 2010-2014 Stig LLC

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

     http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License. */


length = length_of [1, 2, 3, 4, 5];
flength = (length_of x) where {
  x = given::([int]);
};

optint = (v) where {
  v = given::(int?);
};
with {
  <[0]> <- [1, 2, 3];
} test {
  t1: length_of [1, 2, 3, 4, 5] == 5;
  t2: length_of {1, 2, 3, 4, 5} == 5;
  t3: length_of {1:1.1, 2:2.2, 3:3.3, 4:4.4, 5:5.5} == 5;
  t4: (length_of x == 5) where {
    x = [1, 2, 3, 4, 5];
  };
  t5: length == 5;
  t6: length_of empty [int] == 0;
  t7: length_of empty {int} == 0;
  t8: length_of empty {int:bool} == 0;
  t9: length_of ([1, 2, 3, 4, 5]) == 5;
  t10: flength(.x: [1, 2, 3, 4, 5]) == 5;
  t11: length_of ([1..10000] as [int]) == 10000;

  tstring1: length_of "" == 0;
  tstring2: length_of "a" == 1;
  tstring3: length_of "foo" == 3;
  tstring4: length_of ["foo", "bar", "baz", "gak"] == 4;

  tmutable: length_of *<[0]>::([int]) == 3;

  tunknown1: (length_of unknown [int]) is unknown;
  tunknown2: (length_of unknown str) is unknown;

  /* Sets with multiple unknowns should collapse. */
  tsetofunknowns1: length_of ({1 as int?, 2 as int?, unknown int, 3 as int?} | {unknown int}) == 4;
  tsetofunknowns2: length_of ({1 as int?, 2 as int?, unknown int, 3 as int?} | {optint(.v:unknown int)}) == 4;
  tsetofunknowns3: length_of ({1 as int?, 2 as int?, optint(.v:unknown int), 3 as int?} | {unknown int}) == 4;
  tsetofunknowns4: length_of ({1 as int?, 2 as int?, optint(.v:unknown int), 3 as int?} | {optint(.v:unknown int)}) == 4;
};
